Triangle Generator

* This circuit uses an astable multivibrator to generate an approximate square wave
* runs the square wave through a Push-Pull stage to make it more symmetrical
* (otherwise we get a 'low' voltage one diode drop above ground)
* and then the output is integrated using the tlv2231 opamp
* finally the output is again boosted by a second tlv2231 opamp
* to get about 3V swing out

.include ../libs/nabla.lib
.include ../libs/ti-downloads/TLE2426x.LIB ; Voltage rail splitter push/pull
.include ../libs/ti-downloads/LMV7219.MOD ; Comparator
.include ../libs/on_fet.lib ; for the power mosfet

* Now we define the circuit
* Power-supply
VCC vcc 0 5
vcmp vcmp 0 3

* Astable multivibrator ======================================
* left R-C circuit
R1 vcc q_not 100
R2 vcc q_ctrl 10k
C1 q_not q_ctrl 10n

* right R-C circuit
R4 vcc q 100
* optimum seems close to 10.45k
R3 vcc q_not_ctrl 9.5k
C2 q_not_ctrl q 10n

* cross coupled transistors
Q3A q q_ctrl 0 2N3904
Q3B q_not q_not_ctrl 0 2N3904


* Push pull stage ============================================ 
R5 q q_base 10k
* npn transistor on the high side
Q1 vcc q_base q_sym  2N3904
* pnp transistor on the low side
Q4 0 q_base q_sym 2N3906

* Virtual ground ==============================================
* need to be precision resistors
R6 vcc v_gnd_ref 5k
R7 v_gnd_ref 0 5k
Xvgnd v_gnd_ref vcc 0 vgnd TLE2426x
C3 vcc vgnd 10uf
C4 vgnd 0 10uf

* integrating stage =============================================

Xintegrator_opamp oa_in oa_in_inv vcc 0 int_out TLV2371
R8 vgnd oa_in 20k
R9 q_sym oa_in_inv 100k
R10 int_out oa_in_inv 300k
C5 int_out oa_in_inv 2.2n

* amplifier with factor -> 1 + (R12/R11)
Xintegrated_sig_amp_opamp int_out amp_inv_in vcc 0 triangle_wav TLV2371
R11 amp_inv_in vgnd 50k
R12 triangle_wav amp_inv_in 180k


* comparator stage that produces the pwm =========================

* comparator stage
Xcmp triangle_wav vampfdbk vcc 0 pwm TLV2231_TI_5_2 

* feedback to add hysteresis
Rf pwm vfeedback 4MEG


* Inductor Stage with switch and diode ===========================
L1 vcc sw_node 250u
Rd vcc rdamp 10
Cd rdamp sw_node 100n
* diode
Xd sw_node out PMEG4050EP
* switching transistor
Xs sw_node pwm 0 ntd3055-150
CF out 0 500u
* load resistor on the output
RL out 0 100

* Amplifier for feedback voltage =================================
* Output voltage reduced via a Voltage divider
Rvf1 out vfeedback 200k
Rvf2 vfeedback 0 20k

* vref goes to inv it is the thing we are comparing to
* Also either a voltage source or a resistor divider
Rr1 vcc vref 70k
Rr2 vref 0   10k

Xfdbamp vfeedback vref vcc 0 vampfdbk TLV2221_TI_5_2

* feedback circuit
Rctrlfdb vampfdbk vref 40k



* .ic v(out)=0 
* .ic v(q)=0
* .ic v(q_not)=0
* .ic v(q_not_ctrl)=0
* .ic v(q_ctrl)=0
* .ic v(sw_ctrl)=5
* .ic v(triangle_wav)=0

.control
.option savecurrents
tran 500n 20m
set filetype=ascii
write pwm.ngdat v(triangle_wav) v(pwm) v(int_out) v(out) v(sw_node)
plot triangle_wav vfeedback vampfdbk pwm out @L1[i]
plot out @L1[i]
.endc
