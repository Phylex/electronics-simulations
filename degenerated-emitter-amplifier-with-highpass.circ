emitter degenerated bipolar amplifier 

* This circuit is an emitter degenerated bipolar amplifier.
* It uses a highpass filter parallel to the emitter degeneration resistor to enhance the gain
* at larger signal frequencies

* define the npn transistor that we are using
* (otherwise import this part from a library)
* This specific 'part' is created by specifying the parameter values for the
* model being used
.model BC546B npn ( IS=7.59E-15 VAF=73.4 BF=480 IKF=0.0962 NE=1.2665
+ ISE=3.278E-15 IKR=0.03 ISC=2.00E-13 NC=1.2 NR=1 BR=5 RC=0.25 CJC=6.33E-12
+ FC=0.5 MJC=0.33 VJC=0.65 CJE=1.25E-11 MJE=0.55 VJE=0.65 TF=4.26E-10
+ ITF=0.6 VTF=3 XTF=20 RB=100 IRB=0.0001 RBM=10 RE=0.5 TR=1.50E-07)

* define the circuit
* first the power source
VCC vcc 0 20

* amplifying transistor with emitter and collector resistor
R3 vcc intc 10k
Q1 intc intb inte BC546B
R4 inte 0 1K

* highpass connecting the emitter to ground
R5 inte bpc_in 180
C3 bpc_in 0 500n

* resistors for biasing the base
R1 vcc intb 68k
R2 intb 0 10k

* input coupling capacitor with input signal source
Cin intb in 10u
Vin in 0 dc 0 ac 10m 
* Vin in 0 dc 0 ac 10m *sin (0 1m 500)

* output coupling capacitor with load
Cout out intc 10u
RLoad out 0 100k

.end

* the following section defines what we want to simulate using the model
.control
*tran 10u 10m
ac dec 1000000 1 50k
plot out in inte
.endc

